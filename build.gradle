plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id 'net.researchgate.release' version '2.6.0'
    id 'groovy'
    id 'maven-publish'
    id 'jacoco'
}

repositories {
    jcenter()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation platform('org.jetbrains.kotlinx:kotlinx-coroutines-bom:1.3.5')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.reflections:reflections:0.9.12'
    implementation 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.8'

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

gradlePlugin {
    plugins {
        dotnet {
            id = 'com.itiviti.dotnet'
            implementationClass = 'com.itiviti.DotnetPlugin'
        }
        'dotnet-sonar' {
            id = 'com.itiviti.dotnet-sonar'
            implementationClass = 'com.itiviti.DotnetSonarPlugin'
        }
    }
}

pluginBundle {
    website = 'https://www.github.com/Itiviti/gradle-dotnet-plugin'
    vcsUrl = 'https://www.github.com/Itiviti/gradle-dotnet-plugin'
    description = 'radle-dotnet-plugin is a Gradle plugin for sdk project build'
    tags = ['dotnet', 'dotnet-core', 'msbuild', 'sonarqube', 'nunit3', 'nuget', 'gradle', 'gradle-plugin']

    plugins {
        dotnet {
            displayName = 'Gradle Dotnet plugin'
        }

        'dotnet-sonar' {
            displayName = 'Gradle Dotnet Sonar plugin'
        }
    }

    mavenCoordinates {
        groupId = 'com.itiviti.gradle'
        artifactId = 'gradle-dotnet-plugin'
    }
}

project.tasks.afterReleaseBuild.dependsOn project.tasks.publish